BACKUP ~EEex/backup~
AUTHOR ~Bubb~
VERSION ~master~
README ~EEex/readme-EEex.html~

BEGIN ~EEex~
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~ AND FILE_EXISTS ~data/PATCH26.BIF~) ~Game not supported.~

	ACTION_FOR_EACH file IN ACTION OBJECT TRIGGER BEGIN
		COPY ~EEex\patch\%file%.IDS~ ~EEex\patch~
			COUNT_2DA_ROWS 2 rows
			FOR (i = 0; i <= rows; ++i) BEGIN
				READ_2DA_ENTRY i 0 2 "col1"
				READ_2DA_ENTRY i 1 2 "col2"
				DEFINE_ASSOCIATIVE_ARRAY EEex_append_array BEGIN ~%file%~ , ~%col1%~ , ~%col2%~ => ~~ END
			END
		BUT_ONLY
	END

	DEFINE_ACTION_FUNCTION B3_ESCAPE_STRING
		STR_VAR
			str = ~~
		RET
			escaped_str
	BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY to_escape BEGIN ~$~ => 1 ~^~ => 1 ~.~ => 1 ~*~ => 1 ~+~ => 1 ~?~ => 1 ~[~ => 1 ~]~ => 1 ~\~ => 1 END
		OUTER_PATCH_SAVE escaped_str ~%str%~ BEGIN
			limit = BUFFER_LENGTH
			FOR (i = 0; i < limit; ++i) BEGIN
				READ_ASCII i character (1)
				PATCH_IF VARIABLE_IS_SET EVAL ~to_escape_%character%~ BEGIN
					INSERT_BYTES i 1
					WRITE_ASCII i ~\~
					++i
					++limit
				END
			END
		END
	END

	ACTION_PHP_EACH EEex_append_array AS data => ~~ BEGIN
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_1%~ RET data_1_escaped = escaped_str END
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_2%~ RET data_2_escaped = escaped_str END
		APPEND ~%data%.IDS~ ~%data_1% %data_2%~ UNLESS ~^[ %TAB%]*%data_1_escaped%[ %TAB%]+%data_2_escaped%[ %TAB%]*%MNL%*$~
	END

	COPY ~EEex/loader~ ~.~ IF_EXISTS
	COPY ~EEex/copy~ ~override~ IF_EXISTS


BEGIN ~Enable effect menu module - LShift-on-hover to view spells affecting creature~
REQUIRE_COMPONENT ~EEex.tp2~ ~0~ ~This component requires the main component to be installed.~

	COPY ~override/EEex_Modules.lua~ ~override~
		REPLACE_TEXTUALLY ~^\([ %TAB%]+\["B3EffMen"\][ %TAB%]+=[ %TAB%]+\)false~ ~\1true~
	BUT_ONLY


BEGIN ~Enable timer module - Visual indicators for modal actions, contingencies, and spell/item cooldowns~
REQUIRE_COMPONENT ~EEex.tp2~ ~0~ ~This component requires the main component to be installed.~

	COPY ~override/EEex_Modules.lua~ ~override~
		REPLACE_TEXTUALLY ~^\([ %TAB%]+\["B3Timer"\][ %TAB%]+=[ %TAB%]+\)false~ ~\1true~
	BUT_ONLY


BEGIN ~Timer module - Show modal actions (red bar)~
REQUIRE_COMPONENT ~EEex.tp2~ ~2~ ~This component requires the "Enable timer module" component to be installed.~

	COPY ~override/B3Timer.lua~ ~override~
		REPLACE_TEXTUALLY ~^\(B3Timer_ShowModalTimer = \)[^ %TAB%%WNL%]+~ ~\1true~
	BUT_ONLY


BEGIN ~Timer module - Show contingencies (green bar)~
REQUIRE_COMPONENT ~EEex.tp2~ ~2~ ~This component requires the "Enable timer module" component to be installed.~

	COPY ~override/B3Timer.lua~ ~override~
		REPLACE_TEXTUALLY ~^\(B3Timer_ShowContingencyTimer = \)[^ %TAB%%WNL%]+~ ~\1true~
	BUT_ONLY


BEGIN ~Timer module - Show spell/item cooldowns (cyan bar)~
REQUIRE_COMPONENT ~EEex.tp2~ ~2~ ~This component requires the "Enable timer module" component to be installed.~

	COPY ~override/B3Timer.lua~ ~override~
		REPLACE_TEXTUALLY ~^\(B3Timer_ShowCastTimer = \)[^ %TAB%%WNL%]+~ ~\1true~
	BUT_ONLY
