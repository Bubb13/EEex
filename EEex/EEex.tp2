BACKUP ~weidu_external/EEex/backup~
AUTHOR ~Bubb~
VERSION ~v0.11.0-alpha~
README ~EEex/readme-EEex.html~

ALWAYS
	ACTION_IF !VARIABLE_IS_SET EEex_always THEN BEGIN
		INCLUDE ~EEex/lib/always.tph~
		OUTER_SET EEex_always = 1
	END
END

AUTO_TRA ~EEex/language/%s~
LANGUAGE ~English~ ~english~ ~EEex/language/english/setup.tra~

BEGIN @0 LABEL ~B3-EEex-Main~
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~ AND FILE_EXISTS ~data/PATCH26.BIF~) @1000

	ACTION_FOR_EACH file IN ACTION OBJECT TRIGGER BEGIN
		COPY ~EEex/patch/%file%.IDS~ ~EEex/patch~
			COUNT_2DA_ROWS 2 rows
			FOR (i = 0; i <= rows; ++i) BEGIN
				READ_2DA_ENTRY i 0 2 "col1"
				READ_2DA_ENTRY i 1 2 "col2"
				DEFINE_ASSOCIATIVE_ARRAY EEex_append_array BEGIN ~%file%~ , ~%col1%~ , ~%col2%~ => ~~ END
			END
		BUT_ONLY
	END

	DEFINE_ACTION_FUNCTION B3_ESCAPE_STRING
		STR_VAR
			str = ~~
		RET
			escaped_str
	BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY to_escape BEGIN ~$~ => 1 ~^~ => 1 ~.~ => 1 ~*~ => 1 ~+~ => 1 ~?~ => 1 ~[~ => 1 ~]~ => 1 ~\~ => 1 END
		OUTER_PATCH_SAVE escaped_str ~%str%~ BEGIN
			limit = BUFFER_LENGTH
			FOR (i = 0; i < limit; ++i) BEGIN
				READ_ASCII i character (1)
				PATCH_IF VARIABLE_IS_SET EVAL ~to_escape_%character%~ BEGIN
					INSERT_BYTES i 1
					WRITE_ASCII i ~\~
					++i
					++limit
				END
			END
		END
	END

	ACTION_PHP_EACH EEex_append_array AS data => ~~ BEGIN
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_1%~ RET data_1_escaped = escaped_str END
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_2%~ RET data_2_escaped = escaped_str END
		APPEND ~%data%.IDS~ ~%data_1% %data_2%~ UNLESS ~^[ %TAB%]*%data_1_escaped%[ %TAB%]+%data_2_escaped%[ %TAB%]*%MNL%*$~
	END

	COPY ~EEex/loader~ ~.~
	COPY ~EEex/loader/Lua52~ ~.~
	COPY ~EEex/copy~ ~override~


BEGIN @1 LABEL ~B3-EEex-LuaJIT~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~InfinityLoader.ini~ ~.~
		REPLACE_TEXTUALLY ~^\(LuaLibrary=\).*~ ~\1lua51.dll~
		REPLACE_TEXTUALLY ~^\(LuaPatchMode=\).*~ ~\1REPLACE_INTERNAL_WITH_EXTERNAL~
		REPLACE_TEXTUALLY ~^\(LuaVersionExternal=\).*~ ~\15.1~
	BUT_ONLY

	COPY ~EEex/loader/LuaJIT~ ~.~


BEGIN @2 LABEL ~B3-EEex-Module-Effect-Menu~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Effect Menu Module~ key=~Enable~ value=~1~ END
	BUT_ONLY


BEGIN @3 LABEL ~B3-EEex-Module-Empty-Containers~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Empty Container Module~ key=~Enable~ value=~1~ END
	BUT_ONLY


BEGIN @4 LABEL ~B3-EEex-Module-Hotkey~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~override/EEex_Modules.lua~ ~override~
		REPLACE_TEXTUALLY ~^\([ %TAB%]+\["B3Hotkey"\][ %TAB%]+=[ %TAB%]+\)false~ ~\1true~
	BUT_ONLY


BEGIN @5 LABEL ~B3-EEex-Module-Scale~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Scale Module~ key=~Enable~ value=~1~ END
	BUT_ONLY


BEGIN @6 LABEL ~B3-EEex-Module-Time-Step~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Time Step Module~ key=~Enable~ value=~1~ END
	BUT_ONLY


BEGIN @7 LABEL ~B3-EEex-Module-Timer-Main~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1001

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Timer Module~ key=~Enable~ value=~1~ END
	BUT_ONLY
